;
;  IO.MAC - Input/Output Functions
;
;  PAUSE  BIOS-IO  DOS-IO  KEY?  KEY  PAGE  SPACE  EMIT  TYPE
;  SPACES  (CR)  CR  CONSOLE  PRINTER
;

;	PAUSE  ( -- )

	hdr	1,'PAUSE'	; multitasking support
pause:	call	aexec
pause1:	dw	0		; patched by COLD

	cseg

iofn:	dw	bcono		; output vector
	db	03,0ffh		; input char test

;	BIOS-IO  ( -- )		use BIOS for I/O calls

	hdr	1,'BIOS-IO'
biosio:	ld	a,0ffh
	ld	hl,bcono
biosio1:ld	(iofn),hl
	ld	(iofn+cw+1),a
	nextt

;	DOS-IO  ( -- )		use DOS for I/O calls

	hdr	1,'DOS-IO'
dosio:	xor	a
	ld	hl,dcono
	jp	biosio1

;	KEY?  ( -- flag )	(vkeyq) @execute pause

	hdr	1,'KEY?'
keyq:	call	docol
	dw	lit,vkeyq
	dw	aexec
	dw	pause
	dw	exit

;	KEY  ( -- char )	(vkey) @execute pause

	hdr	1,'KEY'
key:	call	docol
	dw	lit,vkey
	dw	aexec
	dw	pause
	dw	exit

;	PAGE  ( -- )		12 emit

	hdr	1,'PAGE'
pagee:	ld	e,ff		; formfeed char
;	jp	space+1
	ignore2

;	SPACE  ( -- )		bl emit

	hdr	1,'SPACE'
space:	ld	e,32
	push	de
;	jp	emit

;	EMIT  ( char -- )	(vemit) @execute 1 out +! pause

	hdr	1,'EMIT'
emit:	call	docol
	dw	lit,vemit
	dw	aexec
	dw	one,outt,pstor
	dw	pause
	dw	exit

;	TYPE  ( c-addr u -- )	0 ?do count emit loop drop

	hdr	1,'TYPE'
typee:	call	docol
	dw	zero
	dw	xqdo,typee2
typee1:	dw	count,emit
	dw	xloop,typee1
typee2:	dw	drop
	dw	exit

;	SPACES  ( +n -- )	0max 0 ?do space loop

	hdr	1,'SPACES'
spacs:	call	docol
	dw	zmax,zero
	dw	xqdo,spacs2
spacs1:	dw	space
	dw	xloop,spacs1
spacs2:	dw	exit

;	(CR)  ( -- c-addr u )	string (CR) 2 c, 13 c, 10 c, 0 c,

	hdr	1,'(CR)'
pcr:	call	count
	db	2,cr,lf,0

;	CR  ( -- )		(cr) type out off

	hdr	1,'CR'
crr:	call	docol
	dw	pcr,typee
	dw	outt,off
	dw	exit

;	CONSOLE  ( -- ) 	(vcon) @ (vemit) !

	hdr	1,'CONSOLE'	; set EMIT to terminal
consol:	ld	hl,(vcon)
	ld	(vemit),hl
	nextt

;	PRINTER  ( -- ) 	(vlst) @ (vemit) !

	hdr	1,'PRINTER'	; set EMIT to printer
prnt:	ld	hl,(vlst)
	ld	(vemit),hl
	nextt

;	end

